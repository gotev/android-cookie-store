apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: "org.jlleitschuh.gradle.ktlint"

Properties properties = new Properties()
if (project.rootProject.file("local.properties").exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

// start - module specific overrides of default values written in manifest.gradle
def bintray_project_name = "android-cookie-store-okhttp"
def library_description = "OkHttp Cookie Jar implementation for java.net.CookieManager"
def library_keywords = ['android', 'cookie', 'store', 'okhttp', 'stack']
// end - module specific overrides

// start - do not modify this if your project is on github
project.ext {
    mavDevelopers = [(properties.getProperty("bintray.user")): (maintainer)]
    mavSiteUrl = "https://github.com/${github_username}/${github_repository_name}"
    mavGitUrl = mavSiteUrl + '.git'
    bugTrackerUrl = mavSiteUrl + '/issues/'
    mavProjectName = bintray_project_name
    mavLibraryLicenses = ["Apache-2.0": 'http://www.apache.org/licenses/LICENSE-2.0.txt']
    mavLibraryDescription = library_description
    mavVersion = library_version
}
// end - do not modify this if your project is on github

group = library_project_group
version = library_version

dependencies {
    // Testing dependencies
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    // Kotlin
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.squareup.okhttp3:okhttp:4.2.2"
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['mavenPublish']
    pkg {
        repo = "maven"
        name = mavProjectName
        desc = library_description
        websiteUrl = mavSiteUrl
        vcsUrl = mavGitUrl
        issueTrackerUrl = bugTrackerUrl
        licenses = library_licenses
        labels = library_keywords
        publicDownloadNumbers = true
        publish = true
    }
}

test {
    useJUnit()
    maxHeapSize = '1G'
}

sourceCompatibility = "8"
targetCompatibility = "8"

apply from: 'https://raw.githubusercontent.com/sky-uk/gradle-maven-plugin/master/gradle-mavenizer.gradle'
